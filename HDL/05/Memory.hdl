// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/Memory.hdl
/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load=1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid and reads 0. Access to any address
 * in the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the Screen
 * and Keyboard chip specifications given in the lectures and the book.
 */
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    Not(in=address[13], out=ntKBD);
    And(a=address[14], b=ntKBD, out=tSCR);
    And(a=load, b=tSCR, out=tSCRload);

    Not(in=address[14], out=tRAM);
    And(a=load, b=tRAM, out=tRAMload);

    RAM16K(in=in, load=tRAMload, address=address[0..13], out=RAM);
    Screen(in=in, load=tSCRload, address=address[0..12], out=SCR);

    Keyboard(out=rKBD);
    Or8Way(in=address[0..7], out=sKBDl);
    Or8Way(in=address[5..12], out=sKBDh);
    Or(a=sKBDl, b=sKBDh, out=sKBD);
    // Mux16(a=rKBD, b=false, sel=sKBD, out=KBD);

    // Mux4Way16(a=RAM, b=RAM, c=SCR, d=KBD, sel[0]=address[13], sel[1]=address[14], out=out);

    Mux8Way16(
        a=RAM, b=RAM, c=RAM, d=RAM,
        e=SCR, f=SCR, g=rKBD, h=false,
        sel[0]=sKBD,
        sel[1]=address[13],
        sel[2]=address[14],
        out=out
    );
}